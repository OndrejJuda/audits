# H-02 Active bidder may reenter into cancelBid from claimAuction to profit at the expense of the contract and original token owner

## Severity

High

## Impact

When the block.timestamp equals the auction end time, both the claimAuction and cancelBid functions can be invoked. This creates a scenario where the winning bidder can abuse the situation to profit by claiming an NFT and reentering the cancelBid function to retrieve their Ether, essentially obtaining the NFT for free. Additionally, other active bidders may also reenter the cancelBid function from their smart contracts to exploit the reentrancy vulnerability and gain Ether equal to the value of their bids. The situation becomes more severe when multiple auctions occur in the same contract, potentially draining Ether from other token auctions.

## Proof of Concept

The vulnerability arises due to overlapping conditions in the [claimAuction](https://github.com/code-423n4/2023-10-nextgen/blob/8b518196629faa37eae39736837b24926fd3c07c/smart-contracts/AuctionDemo.sol#L105), [cancelBid](https://github.com/code-423n4/2023-10-nextgen/blob/8b518196629faa37eae39736837b24926fd3c07c/smart-contracts/AuctionDemo.sol#L125), and [cancelAllBids](https://github.com/code-423n4/2023-10-nextgen/blob/8b518196629faa37eae39736837b24926fd3c07c/smart-contracts/AuctionDemo.sol#L135) functions. All of these functions can be invoked when **block.timestamp == minter.getActionEndTime(_tokenId)**.

```solidity
function claimAuction(uint256 _tokenid) public WinnerOrAdminRequired(_tokenid,this.claimAuction.selector){
    require(block.timestamp >= minter.getAuctionEndTime(_tokenid) && auctionClaim[_tokenid] == false && minter.getAuctionStatus(_tokenid) == true);
    // ...
}

function cancelBid(uint256 _tokenid, uint256 index) public {
    require(block.timestamp <= minter.getAuctionEndTime(_tokenid), "Auction ended");
    // ...
}

function cancelAllBids(uint256 _tokenid) public {
    require(block.timestamp <= minter.getAuctionEndTime(_tokenid), "Auction ended");
    // ...
}
```

The claimAuction method is responsible for transferring the ERC721 token to the winner and refunding bids to other participants. It provides a reentrancy point through the **safeTransferFrom** call and low-level calls using **payable(address).call{value:...}("")**. These mechanisms enable users to easily reenter the contract.

```solidity
function claimAuction(uint256 _tokenid) public WinnerOrAdminRequired(_tokenid,this.claimAuction.selector){
    require(block.timestamp >= minter.getAuctionEndTime(_tokenid) && auctionClaim[_tokenid] == false && minter.getAuctionStatus(_tokenid) == true);
    auctionClaim[_tokenid] = true;
    // ...
    for (uint256 i=0; i< auctionInfoData[_tokenid].length; i ++) {
        if (auctionInfoData[_tokenid][i].bidder == highestBidder && auctionInfoData[_tokenid][i].bid == highestBid && auctionInfoData[_tokenid][i].status == true) {
            IERC721(gencore).safeTransferFrom(ownerOfToken, highestBidder, _tokenid); // @audit-issue reentrancy
            (bool success, ) = payable(owner()).call{value: highestBid}("");
            emit ClaimAuction(owner(), _tokenid, success, highestBid);
        } else if (auctionInfoData[_tokenid][i].status == true) {
            (bool success, ) = payable(auctionInfoData[_tokenid][i].bidder).call{value: auctionInfoData[_tokenid][i].bid}(""); // @audit-issue reentrancy
            emit Refund(auctionInfoData[_tokenid][i].bidder, _tokenid, success, highestBid);
        } else {}
    }
}
```

Both the cancelBid and cancelAllBids functions lack reentrancy protection and do not check for the condition **auctionClaim[_tokenid] == false** as is done in the claimAuction method.

Active bidders can exploit this vulnerability during the auction claim when block.timestamp equals the auction end time. This is facilitated by the absence of checks for the success of low-level function calls in the auction contract, which prevents transactions from reverting due to a lack of Ether in the contract.

### Example

1. Several bidders participate in a token auction using smart contracts.
2. The block.timestamp equals the auction end time.
3. The claimAuction method is triggered.
4. The function iterates through active bids, refunding them to the bidders.
5. Bidders, having their receive functions prepared, reenter the cancelBid method. They not only receive their original bid back but also gain the same amount from the canceled bid.
6. After processing the bidders, it's the winner's turn. The safeTransferFrom method is invoked. Since the winner also used a smart contract for bidding, the **IERC721Receiver#onERC721Received** function is called.
7. The winner reenters the cancelBid method to retrieve their original bid.
8. The function continues, transferring the token to the winner, who now possesses a new ERC721 token and their original bid.

## Tools Used

Manual review

## Recommended Mitigation Steps

I strongly recommend eliminating the existing overlaps between conditions.

```diff
function claimAuction(uint256 _tokenid) public WinnerOrAdminRequired(_tokenid,this.claimAuction.selector){
-   require(block.timestamp >= minter.getAuctionEndTime(_tokenid) && auctionClaim[_tokenid] == false && minter.getAuctionStatus(_tokenid) == true);
+   require(block.timestamp > minter.getAuctionEndTime(_tokenid) && auctionClaim[_tokenid] == false && minter.getAuctionStatus(_tokenid) == true);
    // ...
}
```
